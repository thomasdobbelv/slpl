GRAMMAR

<Program>               ::= <Statement>

<Block>                 ::= "{" <Statement> "}"

<Statement>             ::= <Expression> ";" <Statement>
                        |   <If> <Statement>
                        |   <Empty>

<If>                    ::= "if" "(" <Expression> ")" <Block> <Else>?
<Else>                  ::= "else" (<If>|<Block>)

<Expression>            ::= <Arithmetic Expression>
                        |   <Boolean Expression>

<Arithmetic Expression> ::= <Term> ([+-] <Arithmetic Expression>)?
<Term>                  ::= <Factor> ([*/] <Term>)?
<Factor>                ::= <Value>
                        |   ( <Arithmetic Expression> )
                        |   "-" <Factor>

<Boolean Expression>    ::= <Logical Conjunction> ("||" <Boolean Expression>)?
<Logical Conjunction>   ::= <Atomic Formula> ("&&" <Logical Conjunction>)?
<Atomic Formula>        ::= <Identifier>
                        |   "!" <Atomic Formula>
                        |   "(" <Boolean Expression> ")"
                        |   <Arithmetic Expression> <Relational Operator> <Arithmetic Expression>

<Value>                 ::= <Number>
                        |   <Identifier>

<Number>                ::= [0-9]+(\.[0-9]+)?
<Identifier>            ::= [A-Za-z][A-Za-z0-9]*

<Relational Operator>   ::= "=="
                        |   "!="
                        |   ">="
                        |   "<="
                        |   ">"
                        |   "<"
