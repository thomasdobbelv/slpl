<Program>               ::= <Module>

<Module>                ::= "module" <Identifier> <Block>

<Block>                 ::= "{" <Statement>* "}"

<Statement>             ::= <If>
                        |   <While>
                        |   <For>
                        |   (<Expression>|<Print>|<Declaration>|<Assignment>|<Return Statement>) ";"

<Return Statement>      ::= "return" <Rvalue>

<Lambda Function>       ::= <Parameter List> ":" <Type> "->" <Block>

<Parameter List>        ::= "(" (<Parameter> ",")* ")"
<Parameter>             ::= <Declaration>

<For>                   ::= "for" "(" <Declaration>? ";" <Expression>? ";" <Assignment>? ")" <Block>

<While>                 ::= "while" "(" <Expression> ")" <Block>

<Assignment>            :== <Prefix Operator> <Identifier>
                        |   <Identifier> <Postfix Operator>
                        |   <Identifier> <Assignment Operator> <Rvalue>

<Rvalue>                ::= <Lambda Function>
                        |   <Expression>

<Prefix Operator>       ::= "++"
                        |   "--"

<Postfix Operator>      ::= "++"
                        |   "--"

<Assignment Operator>   ::= "+="
                        |   "-="
                        |   "*="
                        |   "/="
                        |   "="

<Invocation>            ::= "(" (<Expression> ",")* ")"

<Declaration>           ::= <Identifier> ":" <Type> ("=" <Rvalue>)?

<Type>                  ::= <Identifier>
                        |   "(" (<Type> ",")* ")" -> <Type>

<Print>                 ::= println|print (<String>|<Expression>)

<If>                    ::= "if" "(" <Expression> ")" <Block> <Else>?
<Else>                  ::= "else" (<If>|<Block>)

<Expression>            ::= <Term> ((+|-|\|\||<Relational Operator>) <Expression>)?
<Term>                  ::= <Factor> ((*|/|&&) <Term>)?
<Factor>                ::= [!-] <Factor>
                        |   "(" <Expression> ")"
                        |   <Value>

<Value>                 ::= <Number>
                        |   <Boolean>
                        |   <String>
                        |   <Null>
                        |   <Identifier> <Invocation>? <-- invocation NYI

<Number>                ::= [0-9]+(\.[0-9]+)?
<Boolean>               ::= true|false
<String>                ::= ^"[^"\\]*(\\.[^"\\]*)*"$
<Null>                  ::= "null"
<Identifier>            ::= [A-Za-z][A-Za-z0-9]*

<Relational Operator>   ::= "=="
                        |   "!="
                        |   ">="
                        |   "<="
                        |   ">"
                        |   "<"
